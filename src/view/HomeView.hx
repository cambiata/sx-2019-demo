package view;

import data.KorakademinScorxItems;
import js.Browser;
import data.Model;
import data.AppStore;
import view.*;

class HomeView extends AppBaseView {
	// /**
	//  * Första vattendelare för hemsidan
	//  * Ifall userId är null, visa hemsidan för gäst
	//  * Ifall userId är satt, visa hemsidan för inloggad användare
	//  */
	public function view() {
		return this.store.state.userId == null ? this.guestView() : cast this.userView();
	}

	//-------------------------------------------------------------------------------------

	/**
	 * Returnera vy för gäst
	 */
	public function guestView() {
		var freeListItems:Array<ScorxAccessListItem> = KorakademinScorxItems.songs().filter(song -> song.licenseholder == 'Upphovsrättsfri')
			.map(song -> {access: FreeItem(song.scorxProductId, 'Folkbildningsrådet'), song: song});

		return [
			buildCells([
				Image('assets/img/old-town.jpg'),
				Title('Sjung och spela var du vill'),
				Info('ScorX spelare funkar både för mobil och surfplatta!'),
			]),
			buildCells([
				Infoblobs([
					Infoblob.Standard('Har du prövat?', 'Klicka här för att...'),
					Infoblob.Standard('Titta här!', 'Skulle inte du också vilja åka till...'),
					Infoblob.Standard('Vill du veta mer?', 'Undrar du över något? klicka här!')
				]),
				Image('assets/img/happy.jpg'),
				Title('Pröva ScorX gratis!'),
				Info('Klicka på valfri titel i listan nedan, lyssna och sjung med!'),
				// SonglistHeader('Gratis låtar', null),
				// Songlist2(freeListItems),
				Songlist3('Gratis låtar', freeListItems),
			]),
		];
	} //-------------------------------------------------------------------------------------

	/**
	 * Returnera vyer för inloggad användare
	 */
	function userView() {
		return [choirAdminView(), choirMemberView(), userSongsView()];
	}

	function choirMemberView() {
		var user:User = this.store.getUser();

		var groups:Array<Group> = this.store.state.groups.filter(group -> group.members.indexOf(user.username) > -1);

		// filtrera bort så att inte ledarna ser denna vy,
		// de ser ju samma grupp i choirAdminsView()
		groups = groups.filter(group -> {
			!(group.admins.indexOf(user.username) > -1);
		});

		var groupLists = groups.map(group -> {
			var accesses:Array<ScorxAccessListItem> = group.groupSongs.map(access -> ScorxAccessUtils.getAccessListItem(access)).array();
			buildCells([Songlist3(group.name, accesses),]);
		});

		var choirsInfo = groupLists
			.length > 0 ? 'Här visas de låtar som delats ut till dig av dina körer.' : 'Du verkar inte vara deltagare i någon kör eller grupp i ScorX.';

		return [
			// buildCells([
			// 	Infoblobs([
			// 		Infoblob.Standard('Hej Körsångare', 'Klicka här för att...'),
			// 		Infoblob.Standard('1000 låtar gratis!', 'Körakademin Plus är stället för dej!'),
			// 		Infoblob.Standard('Vad betyder "con tenerezza"?', 'Undrar du över något? klicka här!')
			// 	]),

			// ]),
			buildCells([Title('Körernas låtar'), Info(choirsInfo),]),

			groupLists,
			groupLists.length == 0 ? m('div.centerpad', buildCells([SearchChoir])) : null,

			// new UserInvitationsView(this.store).view(),
		];
	}

	function choirAdminView() {
		var user:User = this.store.getUser();

		var groups:Array<Group> = this.store.state.groups.filter(group -> group.admins.indexOf(user.username) > -1);
		if (groups == null || groups.length == 0)
			return null;

		var groupLists = groups.map(group -> {
			var accesses:Array<ScorxAccessListItem> = group.groupSongs.map(access -> ScorxAccessUtils.getAccessListItem(access)).array();

			var groupIsSensus:Bool = this.store.groupIsSensusGroup(group);

			buildCells([
				// SonglistHeader(group.name, null), // Songlist(group.name, KorakademinScorxItems.songs(), [SelectProductIds(group.songs)]),
				// Songlist2(accesses), // ListGroupMembers(group.name),
				Songlist3(group.name, accesses),

				GroupAddSongs(group),
				InviteGroupMembers(group.name),
			]);
		});

		return [
			buildCells([Title('Du leder följande körer'),]),
			groupLists,
			buildCells([AutogeneratedSensusLeaderSonglist]),
		];
	}

	function userSongsView() {
		var user:User = this.store.getUser();

		var myListItems:Array<ScorxAccessListItem> = try {
			user.userSongs.map(access -> {
				// var productId:Int = switch access {
				// 	case FreeItem(scorxProductId, provider): scorxProductId;
				// 	case UserPurchase(scorxProductId, expires): scorxProductId;
				// 	case UserPrivilege(scorxProductId, privilege): scorxProductId;
				// 	case GroupPurchase(scorxProductId, expires, validOnlyForTheseMembers): scorxProductId;
				// 	case GroupPrivilege(scorxProductId, privilege): scorxProductId;
				// }
				// trace(productId);
				var productId = ScorxAccessUtils.getProductId(access);

				var song:ScorxItem = KorakademinScorxItems.getSong(productId);
				var listItem:ScorxAccessListItem = {access: access, song: song};
				return listItem;
			}).array();
		} catch (e:Dynamic) {
			Browser.alert(e);
			[];
		}
		trace(myListItems);

		// var myListItems:Array<ScorxAccessListItem> = [{access: UserPurchase(999), song: KorakademinScorxItems.getSong(999)}];

		return [

			buildCells([
				Title('Mina låtar'),
				Info('Här visas de låtar som du har köpt eller valt genom förmånserbjudanden.'),
				// SonglistHeader('Mina låtar', null),
				// Songlist2(myListItems),
				Songlist3('Mina låtar', myListItems),
				UserAddSongs(user),
				ShowFreeSongsIfNothingElse(user),
			]),
		];
	}

	//-------------------------------------------------------------------------------------
	// Hjälpfunktioner för att skapa element på sidan

	public function buildCells(cells:Array<HomeCell>) {
		function onSongClick(song:ScorxItem) {
			// Browser.alert(song);
			// this.store.update(this.store.state.playerSong = song);
		}

		function onListItemClick(item:ScorxAccessListItem) {
			// trace('click ' + item);
			this.store.update(this.store.state.playerAccessItem = item, 'y');
			this.store.update(this.store.state.playerShow = true, 'x');
		}

		return m('section', cells.map(cell -> {
			return switch cell {
				case Image(url): m('img', {src: url});

				case Title(title): m('h1.center-narrow.vspace', title);

				case Info(info): m('h3.center-narrow.vspace', info);

				case SonglistHeader(title, info):
					m('div.centerpad', [m('h2', title), info != null ? m('p', info) : null]);

				case Songlist2(items):
					m('div.centerpad', new SongListView2(this.store, items, onListItemClick).view());

				case Songlist3(title, items):
					m('div.centerpad', new SongListView3(this.store, title, items, onListItemClick).view());

				case SearchChoir:
					// skapa lista för eventuella gruppansökningar för inloggad användare
					// cast [new UserApplicationsView(this.store).view(),];
					new SearchGroupView(this.store,
						'Skriv körens namn i listan för att hitta din kör. Klicka därefter på körens namn för att ansluta dig till gruppen.', group -> {
						this.store.addGroupMember(this.store.state.userId, group.name);
						// group.admins.map(admin->this.store.sendEmailMessage({to:admin.username, from:'admin@scorx.org', })
						this.store.sendEmailMessage({to: this.store.state.userId, from: 'admin@scorx.org', type: EmailType.UserGroupjoinInfo(group.name)});
						group.admins.map(admin -> {
							this.store.sendEmailMessage({
								to: admin,
								from: 'admin@scorx.org',
								type: EmailType.AdminGroupjoinInfo(this.store.state.userId, group.name)
							});
						});
					}).view();

				case GroupAddSongs(group):
					{
						var isSensusGroup:Bool = this.store.groupIsSensusGroup(group);
						var sensusbutton = isSensusGroup ? m('button.bigger', {
							onclick: e -> this.store.gotoPage(Page.Other)
						}, 'Lägg till via Körakademins förmånserbjudande') : m('div.graytext', 'Ej Sensus-kör, kan ej använda Körakademins grupperbjudande');

						m('div.centerpad', [

							m('button.bigger', {onclick: e -> Browser.alert('Gå till butiken')}, 'Köp fler titlar till ${group.name}'),
							sensusbutton,
						]);
					}

				case UserAddSongs(user): {
						var isSensusUser:Bool = this.store.userIsSensusMember(user);

						var sensusbutton = isSensusUser ? m('button.bigger', {
							onclick: e -> this.store.gotoPage(Page.Other)
						}, 'Lägg till via Körakademins förmånserbjudande') : m('div.graytext', 'Ej Sensus-användare, kan ej använda Körakademins grupperbjudande');

						m('div.center', [
							m('div.centerpad', [

								m('button.bigger', {onclick: e -> Browser.alert('Gå till butiken')}, 'Köp fler titlar till Mina låtar-listan'),

								sensusbutton,
							])
						]);
					}

				case ListGroupMembers(groupname):
					var group:Group = this.store.getGroup(groupname);
					m('div.centerpad', [
						this.detailsSummary('Gruppens medlemmar', [m('ul', group.members.map(member -> m('li', member)))])

					]);

				case InviteGroupMembers(groupname):
					var group:Group = this.store.getGroup(groupname);
					m('div.centerpad', [

						this.detailsSummary('Bjud in medlemmar till $groupname', new LeaderInviteUsers(this.store, group).view())
					]);

				case Infoblobs(blobs):
					m('div.infoblobs-wide', m('div.infoblobs.centerdiv', blobs.map(blob -> {
						return switch blob {
							case Infoblob.Standard(title, info): m('div.infoblob',
									m('div.infoblob-content', [m('div.infoblob-title', title), m('div.infoblob-info', info)]));
						}
					})));

				case AutogeneratedSensusLeaderSonglist:
					var user:User = this.store.getUser();
					var groups:Array<Group> = this.store.getLeaderGroups(user.username);
					var sensusGroups:Array<Group> = groups.filter(group -> this.store.groupIsSensusGroup(group));
					var showThisList = sensusGroups.length > 0;

					if (!showThisList) return m('p.centerpad',
						'(Denna körledare ser INTE Sensus Körledarlista - beroende på att hen leder inte någon grupp där samtliga admins har sensus:SensusUser satt.)');

					var items:Array<ScorxAccessListItem> = KorakademinScorxItems.songs()
						.map(song -> {song: song, access: GroupPrivilege(song.scorxProductId, null)});

					m('div.centerpad', [
						m('h1.center-narrow.vspace', 'Körakademins samtliga låtar'),
						m('h3.center-narrow.vspace',
							'(Denna lista därför att användaren leder någon kör som är kopplad till Sensus körverksamhet, dvs. samtliga ledare i någon av grupperna har sensus:SensusUser satt.)'),
						new SongListView2(this.store, items, onListItemClick).view(),
					]);

				case ShowFreeSongsIfNothingElse(user):
					if (user.userSongs.length > 0) return null;

					if (this.store.getUserGroups(user.username).length > 0) return null;

					if (this.store.getLeaderGroups(user.username).length > 0) return null;

					var freeListItems:Array<ScorxAccessListItem> = KorakademinScorxItems.songs().filter(song -> song.licenseholder == 'Upphovsrättsfri')
						.map(song -> {access: FreeItem(song.scorxProductId, 'Folkbildningsrådet'), song: song});

					m('div.centerpad', [
						m('h1.center-narrow.vspace', 'Gratislåtarna igen'),
						m('h3.center-narrow.vspace',
							'(Denna lista visas för att användaren varken har låtar i Mina låtar-listan, eller fått tilldelat genom någon kör.)'),

						new SongListView2(this.store, freeListItems, onListItemClick).view(),
					]);

				case AutogeneratedNewsList: null;
				case AutogeneratedHistoryList: null;
			}
		}));
	}
}
